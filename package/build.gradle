import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java'
apply plugin: 'application'

assert Runtime.version().major() == 12
sourceCompatibility = 12
targetCompatibility = 12

mainClassName = 'com.uddernetworks.mspaint.main.JFXWorkaround'

ext {
    publishedVersion = rootProject.version

    libraryPath = file("${rootProject.buildDir}/libraries")
    mainJar = rootProject.jar.outputs.files[0].name
    bundlePath = file("${buildDir}/bundle")

    // In-bundle resources
    windowsExecutable = file("${bundlePath}/MSPaintIDE/MSPaintIDE.exe")
    bundleContent = file("${bundlePath}/MSPaintIDE/*")
    bundleReadme = file("src/main/resources/README.html")
    appIcon = file('src/main/resources/icons/')

    generatedInnoSetupFile = file("${buildDir}/resources/main/MSPaintIDE.iss")

    bundlePath.deleteDir()
    bundlePath.mkdirs()
}

jar {
    manifest {
        attributes(
                'Main-Class': mainClassName
        )
    }
}

// Happens during assembly
task jpackage(type: Exec, dependsOn: ':copyDependencies') {
    def modules = [
            'java.management',
            'java.desktop',
            'java.compiler',
            'javafx.graphics',
            'javafx.controls',
            'javafx.fxml',
            'java.sql',
            'java.naming'
    ]

    commandLine "${project.properties['jpackage.home']}/bin/jpackage",
            'create-app-image',
            '--verbose',
            '--module-path', 'C:\\javafx-jmods-12.0.1',
            '--add-modules', modules.join(','),
            '--name', 'MSPaintIDE',
            '--icon', "${appIcon}/icon.ico",
            '--app-version', publishedVersion,
            '--description', 'MS Paint IDE',
            '--vendor', 'Adam Yarris',
            '--input', libraryPath,
            '--output', bundlePath,
            '--main-jar', mainJar,
            '--main-class', mainClassName,
            '--java-options', '--add-opens javafx.base/com.sun.javafx.reflect=ALL-UNNAMED --add-opens java.base/jdk.internal.loader=ALL-UNNAMED --add-opens java.base/jdk.internal.loader=ALL-UNNAMED --add-opens javafx.graphics/com.sun.javafx.scene=ALL-UNNAMED --add-opens javafx.base/com.sun.javafx.collections=ALL-UNNAMED --illegal-access=deny'
    environment << [JPACKAGE_DEBUG: true]
}

// Copies dependencies and resources to the bundle path
task completeInnoSetup {
    doLast {
        println "From " + "${rootProject.projectDir}\\package/src/main/java/resources to ${bundlePath}/MSPaintIDE"
        copy {
            from "${rootProject.projectDir}\\package\\src\\main\\resources"
            into "${bundlePath}\\MSPaintIDE"
            exclude 'MSPaintIDE.iss'
            exclude 'icons'
        }

        copy {
            from "${rootProject.projectDir}/native/"
            into "${bundlePath}/MSPaintIDE/native"
            include 'PaintInjector.dll'
        }

        println "Inno Setup file: ${generatedInnoSetupFile}"
    }
}

// Replaces values in the .iss file
processResources {
    filesMatching('MSPaintIDE.iss') {
        filter ReplaceTokens, tokens: [
                'bundle.version'     : publishedVersion,
                'executable.location': windowsExecutable.toString(),
                'bundle.content'     : bundleContent.toString(),
                'bundle.readme'     : bundleReadme.toString(),
                'icon.directory'          : appIcon.toString()
        ]
    }
}

// Actually compiles the installer
task innoCompile(type: Exec) {
    executable 'C:\\Program Files (x86)\\Inno Setup 5\\compil32.exe'
    args "/cc", "\"${generatedInnoSetupFile}\""
    doLast {
        task copyOutput(dependsOn: processResources) {
            doLast {
                copy {
                    from "${buildDir}\\resources\\main\\Output"
                    into "${buildDir}"
                    include "MSPaintIDE-${publishedVersion}.exe"
                }
            }
        }

        copyOutput.execute()
        println "Installation file: ${buildDir}\\MSPaintIDE-${publishedVersion}.exe"
    }
}

assemble.dependsOn jpackage
